# Maintainer: Terromur <terromuroz@proton.me>
# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
## Maintainers of original Arch Linux package:
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mingw-w64-gcc
pkgver=15.1.0
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64 cross-compiler"
arch=('x86_64' 'aarch64')
url="https://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
makedepends=('gmp' 'mpfr' 'mpc')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=('zlib' 'libisl' 'libmpc' 'mingw-w64-crt' 'mingw-w64-binutils' 'mingw-w64-winpthreads' 'mingw-w64-headers')
provides=('mingw-w64-gcc')
conflicts=('mingw-w64-gcc')
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
        '0020-libgomp-Don-t-hard-code-MS-printf-attributes.patch'
        'clearlinux.patch'
        '0140-gcc-diagnostic-color.patch'
        '0014-gcc-9-branch-clone_function_name_1-Retain-any-stdcall-suffix.patch'
        '0008-Prettify-linking-no-undefined.patch'
 	'9001-Always-quote-labels-in-Intel-syntax.patch'
	'9003-libstdc-Avoid-thread-local-states-for-MCF-thread-mod.patch'
	'0021-PR14940-Allow-a-PCH-to-be-mapped-to-a-different-addr.patch'
	'3001-fix-codeview-crashes.patch'
	)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  ln -sf gcc-${pkgver/+/-} gcc

  cd gcc

  # Adds performance boost for DXVK/Wine (maybe)
  patch -Np1 -i "${srcdir}/0020-libgomp-Don-t-hard-code-MS-printf-attributes.patch"
  patch -Np1 -i "${srcdir}/clearlinux.patch"
  patch -Np1 -i "${srcdir}/0140-gcc-diagnostic-color.patch"
  patch -Np1 -i "${srcdir}/0014-gcc-9-branch-clone_function_name_1-Retain-any-stdcall-suffix.patch"
  patch -Np1 -i "${srcdir}/0008-Prettify-linking-no-undefined.patch"
  patch -Np1 -i "${srcdir}/9001-Always-quote-labels-in-Intel-syntax.patch"
  patch -Np1 -i "${srcdir}/9003-libstdc-Avoid-thread-local-states-for-MCF-thread-mod.patch"
  patch -Np1 -i "${srcdir}/0021-PR14940-Allow-a-PCH-to-be-mapped-to-a-different-addr.patch"
  patch -Np1 -i "${srcdir}/3001-fix-codeview-crashes.patch"
  # mmapio.c:69:14: error: implicit declaration of function ‘getpagesize’
  sed -i 's|\-Werror||g' libbacktrace/configure
}


build() {
# Добавлено для уверенности стабильной работы для lgomp
export lt_cv_deplibs_check_method='pass_all'
# In addition adaint.c does `#include <accctrl.h>` which pulls in msxml.h, hacky hack:
CPPFLAGS+=" -DCOM_NO_WINDOWS_H"
export CFLAGS="-march=x86-64-v3 -mtune=haswell -O2 -fdata-sections -funsafe-math-optimizations -fno-ident -pthread -fopenmp -fno-math-errno -fno-trapping-math -fno-semantic-interposition -fivopts -ffunction-sections -fprofile-correction -floop-nest-optimize -fgraphite-identity -floop-strip-mine -fmodulo-sched -flive-range-shrinkage -fno-stack-protector -fcf-protection=none -mharden-sls=none -fomit-frame-pointer -fno-unwind-tables"
export CXXFLAGS="$CFLAGS -Wp,-U_GLIBCXX_ASSERTIONS"
export LDFLAGS="-Wl,-O3 -Wl,-Bsymbolic-functions,--sort-common,--as-needed,--strip-all -lpthread -Wl,--gc-sections"
  for _arch in ${_architectures}; do
    mkdir -p "$srcdir/build-${_arch}" && cd "$srcdir/build-${_arch}"
    "$srcdir"/gcc/configure --prefix=/usr --libexecdir=/usr/lib \
        --target="${_arch}" \
        --enable-languages=c,c++,lto,objc,obj-c++ \
        --enable-shared --enable-static \
        --enable-threads=posix --enable-fully-dynamic-string \
        --enable-libstdcxx-time=yes --enable-libstdcxx-filesystem-ts=yes \
        --with-system-zlib --enable-cloog-backend=isl \
        --enable-lto --enable-libgomp \
        --enable-graphite \
        --enable-plugin \
        --disable-multilib --enable-checking=release \
        --disable-sjlj-exceptions --with-dwarf2 \
        --disable-libstdcxx-debug \
	--disable-ssp \
        --disable-win32-registry
        
	# Добавлено для уверенности стабильной работы -fopenmp
        sed -i 's,^STRIP = .*$,STRIP = true,g'                   Makefile
        sed -i 's,^STRIP_FOR_TARGET=.*$,STRIP_FOR_TARGET=true,g' Makefile

	  make -O \
          BOOT_CFLAGS="$CFLAGS" \
          BOOT_CXXFLAGS="$CXXFLAGS" \
          BOOT_LDFLAGS="$LDFLAGS" \
          BOOT_ASFLAGS="$ASFLAGS" \
          CFLAGS_FOR_TARGET="$CFLAGS" \
          CXXFLAGS_FOR_TARGET="$CXXFLAGS" \
          LDFLAGS_FOR_TARGET="$LDFLAGS" \
          ASFLAGS_FOR_TARGET="$ASFLAGS"; done
          
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/build-${_arch}"
    make DESTDIR="$pkgdir" install

    "${_arch}"-strip "$pkgdir/usr/${_arch}/lib"/*.dll
    strip "$pkgdir"/usr/bin/"${_arch}"-*
    strip "$pkgdir/usr/lib/gcc/${_arch}/${pkgver}"/{cc1*,collect2,lto*}
    ln -s ${_arch}-gcc "$pkgdir"/usr/bin/${_arch}-cc

    # mv dlls
    mkdir -p "$pkgdir"/usr/"${_arch}"/bin/
    mv "$pkgdir/usr/${_arch}/lib"/*.dll "$pkgdir/usr/${_arch}/bin/"
  done

  strip "$pkgdir"/usr/bin/*

  # remove unnecessary files
  rm -r "$pkgdir"/usr/share
  rm "$pkgdir"/usr/lib/libcc1.*
}
