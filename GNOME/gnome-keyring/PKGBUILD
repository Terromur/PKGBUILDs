# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring
pkgver=46.2
pkgrel=1
epoch=1
pkgdesc="Stores passwords and encryption keys"
url="https://gitlab.gnome.org/GNOME/gnome-keyring"
arch=(x86_64)
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  dconf
  gcr
  gcr-4
  glib2
  glibc
  libgcrypt
  pam
  systemd-libs
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  p11-kit
  python
)
provides=(org.freedesktop.secrets)
groups=(gnome)
install=gnome-keyring.install
source=(
  "git+$url.git#tag=$pkgver"
  bsc932232-use-libgcrypt-allocators.patch
  bsc932232-use-non-fips-md5.patch
  sys-select-h.patch
)
b2sums=('42253abd7684fc4f37ab644e0cabed0b4b4549e859f1a3cc81a0a97a0ddacb6dfa29dac0a926d0901be4fd2ad01352091aa19d1a2057a0116af062b8089b1ac7'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd $pkgname

  git apply -3 ../bsc932232-use-libgcrypt-allocators.patch
  git apply -3 ../bsc932232-use-non-fips-md5.patch
  git apply -3 ../sys-select-h.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --with-pam-dir=/usr/lib/security
    --without-libcap-ng
    --disable-static
    --disable-schemas-compile
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  # Secure memory tests fail
  dbus-run-session make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
