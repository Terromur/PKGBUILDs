# Maintaier: Terromur <terromuroz@proton.me>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=wireplumber
pkgname=(
  wireplumber
  libwireplumber
)
pkgver=0.5.7.r10.ga1bc3d9
pkgrel=1
pkgdesc="Session / policy manager implementation for PipeWire"
url="https://pipewire.pages.freedesktop.org/wireplumber/"
arch=(x86_64)
license=(MIT)
makedepends=(
  doxygen
  git
  glib2
  gobject-introspection
  graphviz
  lua
  meson
  pipewire
  python-lxml
  systemd
)
checkdepends=(pipewire-audio)
source=("git+https://gitlab.freedesktop.org/pipewire/$pkgbase.git")
b2sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
      -D b_ndebug=false \
      -D b_pie=false \
      -D c_std=gnu2x \
      -D cpp_std=gnu++23 \
      --buildtype=release \
      --wrap-mode=nofallback \
      -D doc=disabled \
      -D elogind=disabled \
      -D system-lua=true \
      -D tests=false \
      -D dbus-tests=false \
      -D errorlogs=false \
      -D python.bytecompile=2 \
      -D build.c_std=gnu2x \
      -D b_lto=true \
      -D optimization=3 \
      -D debug=false
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_ver=${pkgver:0:3}

package_wireplumber() {
  depends=(
    "libwireplumber=$pkgver-$pkgrel"
    libsystemd.so
    lua
    pipewire
  )
  provides=(pipewire-session-manager)
  conflicts=(pipewire-media-session)
  install=wireplumber.install

  meson install -C build --destdir "$pkgdir"

  (
    cd "$pkgdir"

    _pick libw usr/lib/libwireplumber-$_ver.so*
    _pick libw usr/lib/girepository-1.0
    _pick libw usr/lib/pkgconfig
    _pick libw usr/include
    _pick libw usr/share/gir-1.0
  )

  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 $pkgbase/{NEWS,README}*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/LICENSE
}

package_libwireplumber() {
  pkgdesc+=" - client library"
  depends=(
    libg{lib,module,object,io}-2.0.so
    libpipewire-0.3.so
  )
  provides=(libwireplumber-$_ver.so)

  mv libw/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/LICENSE
}

# vim:set sw=2 sts=-1 et:
