# Maintainer: Terromur <terromuroz@proton.me>
# Maintainer: GreyXor <greyxor@protonmail.com>
# Contributor: Taran Lynn <taranlynn0gmail.com>
pkgname=swaybg-git
pkgver=1.2.1.r11.gd7ec784
pkgrel=1
pkgdesc="Wallpaper tool for Wayland compositors (git development version)"
arch=('x86_64')
url="https://github.com/swaywm/swaybg"
license=("MIT")
depends=(
"cairo"
"glib2"
"glibc"
"wayland"
)
makedepends=(
'meson'
"git"
"scdoc"
"wayland-protocols"
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}.git")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  local meson_options=(
    --buildtype=release
    -Dpython.bytecompile=2
    -Db_pie=false
    -D debug=false
    -D optimization=3
    -D strip=true
    -D b_lto=true
    -D c_std=gnu23
    -D build.c_std=gnu23
    -D cpp_std=gnu++23
    -D build.cpp_std=gnu++23
    -D errorlogs=false
    -D stdsplit=false
    -D man-pages=disabled
    -D gdk-pixbuf=disabled
  )
  arch-meson "$pkgname" build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${pkgname}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
