# Maintainer: Terromur <terromuroz@proton.me>
_pkgbasename=ghostty
pkgname=${_pkgbasename}-git
pkgrel=1
pkgver=r9256.71e62f96
pkgdesc="Fast, native, feature-rich terminal emulator"
arch=('x86_64')
url="https://github.com/ghostty-org/${_pkgbasename}"
provides=('ghostty')
conflicts=('ghostty' 'ghostty-git')
license=('MIT')
depends=(
	'bzip2'
	'fontconfig'
	'freetype2'
	'gtk4'
	'harfbuzz'
	'libadwaita'
	'libpng'
	'oniguruma'
	'pixman'
	'zlib'
)
makedepends=('git' 'zig>=0.13.0' 'zig<0.14.0' 'pandoc-cli')
source=("git+https://github.com/ghostty-org/${_pkgbasename}")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgbasename}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
        cd "${srcdir}/${_pkgbasename}"

	ZIG_GLOBAL_CACHE_DIR="${srcdir}/tmp" ./nix/build-support/fetch-zig-cache.sh
	zig build --system "${srcdir}/tmp/p" -Dcpu=native -Doptimize=ReleaseFast -Dstrip=true -Dpie=false
}

package() {
	cd "${srcdir}/${_pkgbasename}"
	zig build -p "${pkgdir}"/usr --system "${srcdir}/tmp/p" -Dcpu=native -Doptimize=ReleaseFast -Dstrip=true -Dpie=false
}
