# Maintainer: Terromur <terromuroz@proton.me>
# Maintainer: David Runge <dvzrv@archlinux.org> 
# Contributor: Dan McGee <dan@archlinux.org> 
# Contributor: Martin Striz <ms@poruba.net>
pkgname=irqbalance-git
pkgver=1.9.4.r65.g9322ac1
pkgrel=1
pkgdesc="A daemon to help balance the CPU load generated by interrupts across all of a systems CPUs"
arch=('i686' 'x86_64')
url="https://irqbalance.github.io/irqbalance/"
license=('GPL')
depends=('glibc' 'numactl' 'systemd-libs')
makedepends=('git' 'glib2' 'libcap-ng' 'ncurses' 'systemd')
provides=("irqbalance=$pkgver")
conflicts=('irqbalance')
backup=('etc/irqbalance.env')
source=("git+https://github.com/Irqbalance/irqbalance.git"
"Set-fd-limit.patch"
"fix-service-paths.patch")
sha256sums=('SKIP' 'SKIP' 'SKIP')


prepare() {
  patch -Np1 -i ../Set-fd-limit.patch
  # fix location of configuration and binary in service
  patch -Np1 -i ../fix-service-paths.patch
}

pkgver() {
  cd "irqbalance"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson irqbalance build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    glib2 libglib-2.0.so
    libcap-ng libcap-ng.so
    ncurses libncursesw.so
    numactl libnuma.so
    systemd-libs libsystemd.so
  )

  meson install -C build --destdir "$pkgdir"
  install -vDm 644 irqbalance/misc/irqbalance.service -t "$pkgdir/usr/lib/systemd/system/"
  install -vDm 644 irqbalance/misc/irqbalance.env -t "$pkgdir/etc/"
  install -vDm 644 irqbalance/{AUTHORS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
