# Maintainer: Terromur <terromuroz@proton.me>
# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
# Arch Linux's maintainers:
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: philefou <tuxication AT gmail DOT com>
# Contributor: lindquist <tomas@famolsen.dk>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Mihai Militaru <mihai.militaru@ephemeros.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=glfw-minecraft-wayland
pkgdesc="A free, open source, portable framework for graphical application development"
pkgver=3.4
pkgrel=2
arch=('x86_64')
conflicts=('glfw')
replaces=('glfw-wayland' 'glfw-pure' 'glfw')
provides=('glfw-wayland' 'glfw-pure' 'glfw')
url="https://www.glfw.org/"
license=('custom:ZLIB')
depends=('libgl' 'libxkbcommon')
makedepends=(
    mesa
    cmake
    doxygen
    vulkan-headers
    vulkan-icd-loader
    extra-cmake-modules
    wayland-protocols
)
source=(
    "git+https://github.com/llyyr/glfw-minecraft-wayland.git"
    0004-Fix-Window-size-on-unset-fullscreen.patch
    0005-Add-warning-about-being-an-unofficial-patch.patch
    0007-Fix-fullscreen-location.patch
)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

prepare() {
    cd "glfw-minecraft-wayland"
    patch -Np1 -i "${srcdir}"/0004-Fix-Window-size-on-unset-fullscreen.patch
    patch -Np1 -i "${srcdir}"/0005-Add-warning-about-being-an-unofficial-patch.patch
    patch -Np1 -i "${srcdir}"/0007-Fix-fullscreen-location.patch
}

build() {
  cmake -B build -S "glfw-minecraft-wayland" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGLFW_BUILD_EXAMPLES=NO \
    -DGLFW_BUILD_X11=NO \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "glfw-minecraft-wayland"/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
