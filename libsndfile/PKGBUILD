# Maintainer: Terromur <terromuroz@proton.me>

pkgname=libsndfile
pkgver=1.2.2
pkgrel=2
pkgdesc="A C library for reading and writing files containing sampled audio data"
arch=(x86_64)
url="https://libsndfile.github.io/libsndfile/"
license=(LGPL-2.1-or-later)
depends=(glibc)
makedepends=(
  alsa-lib
  cmake
  flac
  lame
  libogg
  libvorbis
  mpg123
  opus
  python
  sqlite
)
optdepends=('alsa-lib: for sndfile-play')
provides=(libsndfile.so)
source=(https://github.com/$pkgname/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz
        1.2.2-CVE-2022-33065.patch
        CVE-2024-50612.patch)
b2sums=('SKIP' 'SKIP' 'SKIP')
prepare() {
  patch -d $pkgname-$pkgver -Np1 -i ../1.2.2-CVE-2022-33065.patch
  patch -d $pkgname-$pkgver -Np1 -i ../CVE-2024-50612.patch
}

build() {
  local cmake_options=(
    -B build
    -D BUILD_SHARED_LIBS=ON
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D ENABLE_EXTERNAL_LIBS=ON
    -D ENABLE_MPEG=ON
    -S $pkgname-$pkgver
    -W no-dev
  )
  local cmake_test_options=(
    -B build-test
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_test_options[@]}"
  cmake --build build-test --verbose
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    flac libFLAC.so
    lame libmp3lame.so
    libogg libogg.so
    libvorbis libvorbis.so libvorbisenc.so
    mpg123 libmpg123.so
    opus libopus.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
